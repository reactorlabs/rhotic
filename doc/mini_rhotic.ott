% NOTE: Some of the typesetting refers to macros defined in notes.tex
% This is fine, as long as the generated definitions are only used with notes.tex

metavar x       ::= {{ com metavariables }} {{ tex \mathit{[[x]]} }}

metavar l       ::= {{ com labels }} {{ lex numeral }} {{ tex \mathit{[[l]]} }}

metavar num     ::= {{ com numbers }} {{ lex numeral  }}
metavar chr     ::= {{ com character sequences }} {{ lex alphanum }}

metavar a       ::= {{ com address }} {{ tex \mathit{[[a]]} }}

indexvar n      ::= {{ com index variables }}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
grammar

%%% CONCRETE GRAMMAR %%%

%%% Values %%%
bool {{ tex \mathsf{true} ~|~ \mathsf{false} ~|~ \bna }} :: 'bool_' ::=
                                    {{ com boolean literals }}
    | true          :: :: True          {{ tex \mathsf{true} }}
    | false         :: :: False         {{ tex \mathsf{false} }}
    | NA_b          :: :: NA            {{ com boolean \na }} {{ tex \bna }}

int {{ tex \cdots ~|~ -1 ~|~ 0 ~|~ 1 ~|~ \cdots ~|~ \ina }} :: 'int_' ::=
                                    {{ com integer literals }}
    | num           :: :: Int
    | ~ num         :: :: NegInt        {{ tex - [[num]] }}
    | NA_i          :: :: NA            {{ com integer \na }} {{ tex \ina }}

str {{ tex \cdots ~|~ \sna }} :: 'str_' ::=
                                    {{ com string literals }}
    | " chr "       :: :: Str           {{ tex ``[[chr]]" }}
    | NA_s          :: :: NA            {{ com string \na }} {{ tex \sna }}

v :: 'v_' ::=                       {{ com values $\vals$ }}
    | bool          :: :: Bool          {{ com \bvals }}
    | int           :: :: Int           {{ com \ivals }}
    | str           :: :: Str           {{ com \svals }}

%%% Simple expressions %%%
se :: 'se_' ::=                     {{ com simple expressions }}
    | v             :: :: Val           {{ com $[[v]] \in \vals$ }}
    | x             :: :: Var           {{ com $[[x]] \in \vars $ }}

%%% Unary operators %%%
uop :: 'uop_' ::=                   {{ com unary operators }} {{ tex \circ }}
    | !             :: :: Negate        {{ com logical negation }}
    | +             :: :: Plus
    | -             :: :: Minus

%%% Binary operators %%%a
bop :: 'bop_' ::=                   {{ com binary operators }} {{ tex \diamond }}
    | +             :: :: Plus
    | -             :: :: Minus
    | *             :: :: Times
    | /             :: :: Divide
    | %             :: :: Modulo        {{ com modulo }}
    | <             :: :: Less
    | <=            :: :: LessEqual
    | >=            :: :: GreaterEqual
    | >             :: :: Greater
    | =             :: :: Equal
    | =/=           :: :: NotEqual
    | /\            :: :: And           {{ com logical and }}
    | \/            :: :: Or            {{ com logical or }}

%%% Expressions %%%
e :: 'e_' ::=                           {{ com expressions }}
    | isNA se           :: :: IsNA          {{ tex [[isNA]]([[se]]) }}
    | uop se            :: :: UOp
        {{ com $[[uop]] \in \{ \neg, +, - \}$ }}
        {{ tex [[uop]][[se]] }}
    | se1 bop se2       :: :: BOp
        {{ com $[[bop]] \in \{ +, -, \times, \div, \%, <, \leq, =, \neq, >, \geq, \lor, \land \}$ }}
        {{ tex [[se1]] [[bop]] [[se2]] }}
    | se                :: :: SimpleExp

%%% Statements %%%
s :: 's_' ::=                                       {{ com statements }}
    | x <- e                        :: :: Assign
        {{ com $x \in \vars$ }}
    | x <- l ^ input()              :: :: Input
        {{ com $[[l]] \in \labels$ }}
        {{ tex [[x]] [[<-]]\, ^{[[l]]} [[input()]] }}
    | if se then s1 else s2 fi      :: :: If
        {{ tex [[if]]~[[se]]~[[then]]~[[s1]]~[[else]]~[[s2]]~[[fi]] }}
    | for x in se1 : se2 do s od    :: :: For
        {{ tex [[for]]~[[x]]~[[in]]~[[se1]]\negmedspace:\negmedspace[[se2]]~[[do]]~[[s]]~[[od]] }}
    | s1 ; s2                       :: :: Seq
    | e                             :: :: Exp

%%% Program %%%
P :: 'P_' ::=                                       {{ com program }}
    | s                             :: :: Statement

%%% Recorded simple expressions %%%
rse {{ tex \overline{se} }} :: 'rse_' ::=           {{ com recorded simple expressions }}
    | ( se , v )        :: :: SE

%%% Recorded expressions %%%
re {{ tex \overline{e} }} :: 're_' ::=              {{ com recorded expressions }}
    | isNA rse                  :: :: IsNA              {{ tex [[isNA]]([[rse]]) }}
    | uop rse                   :: :: UOp
        {{ com $[[uop]] \in \{ \neg, +, - \}$ }}
        {{ tex [[uop]][[rse]] }}
    | rse1 bop rse2             :: :: BOp
        {{ com $[[bop]] \in \{ +, -, \times, \div, \%, <, \leq, =, \neq, >, \geq, \lor, \land \}$ }}
        {{ tex [[rse1]] [[bop]] [[rse2]] }}
    | rse                       :: :: SimpleExp

%%% Recorded statements %%%
rs {{ tex \overline{s} }} :: 'rs_' ::=              {{ com recorded statements }}
    | x <- ( re , v )                           :: :: Assign
        {{ com $x \in \vars$ }}
        {{ tex [[x]] [[<-]] ([[re]],[[v]]) }}
    | x <- ( l ^ input() , v )                  :: :: Input
        {{ com $[[l]] \in \labels$ }}
        {{ tex [[x]] [[<-]]\, ( ^{[[l]]} [[input()]], [[v]] ) }}
    | if rse then . else . fi                   :: :: If
        {{ tex [[if]]~[[rse]]~[[then]] \bullet [[else]] \bullet [[fi]] }}
        {{ com $\bullet$ is not recorded in this statement }}
    | for ( x , v ) in rse1 : rse2 do . od      :: :: For
        {{ tex [[for]]~([[x]], [[v]])~[[in]]~[[rse1]]\negmedspace:\negmedspace[[rse2]]~[[do]] \bullet [[od]] }}
        {{ com $\bullet$ is not recorded in this statement }}
    | ( re , v )                                :: :: Exp

%%% Environment %%%
E :: 'Env_' ::=                                     {{ com environment }}
    | empty                         :: :: Empty         {{ com empty environment }}
    | E [ x -> v ]                  :: :: UpdateEnv     {{ com environment update }}

%%% Input stream %%%
I :: 'I_' ::=                                       {{ com input stream }}
    | empty                         :: :: Empty         {{ com empty stream }}
    | v . I                         :: :: Single        {{ tex [[v]] \cdot [[I]] }}

%%% Machine configuration %%%
C :: 'C_' ::=                                       {{ com machine configuration }}
    | s I E                         :: :: sIE           {{ tex [[s]]\enspace[[I]]\,[[E]] }}

%%% Label set %%%
L :: 'L_' ::=                                       {{ com label set }}
    | empty                         :: :: Empty         {{ com empty label set }}
    | { l1 , ... , ln }             :: :: Set           {{ com labels }}
    | L1 union L2                   :: :: Intersection  {{ com union }}

%%% Recorded state %%%
r :: 'r_' ::=                                       {{ com recorded state }}
    | [ rs E ]                      :: :: RecordedState
        {{ com recorded statement and environment }}
        {{ tex \langle [[rs]], [[E]] \rangle }}

%%% Execution trace %%%
T :: 'T_' ::=                                       {{ com execution trace }}
    | empty                         :: :: Empty         {{ com empty trace }}
    | r                             :: :: Single        {{ com singleton }} {{ tex [[r]] }}
    | T . r                         :: :: Append        {{ com append }} {{ tex [[T]]\!\cdot\![[r]] }}
    | r . T                         :: :: Prepend       {{ com prepend }} {{ tex [[r]]\!\cdot\![[T]] }}

%%% Recorded machine configuration %%%
rC {{ tex \overline{C} }} :: 'rC_' ::=
    | C T                           :: :: CT


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% ANALYSIS GRAMMAR %%%
Lat :: 'Lat_' ::=                                   {{ com lattice }}
    | alpha ( v )                   :: :: alpha         {{ com abstraction of concrete value }} {{ tex \alpha([[v]]) }}
    | Auop Lat                      :: :: Auop          {{ com abstract unary operation }}
    | Lat1 Abop Lat2                :: :: Abop          {{ com abstract binary operation }}
    | Lat1 lub Lat2                 :: :: Lub           {{ com least upper bound }}
    | Lat1 glb Lat2                 :: :: Glb           {{ com greatest lower bound }}
    | Top                           :: :: Top           {{ com top }}
    | Bot                           :: :: Bot           {{ com bot }}
    % NA lattice
    | NA                            :: :: NA            {{ com \na lattice }} {{ tex \textsc{na} }}
    | ! NA                          :: :: NotNA         {{ com \na lattice }} {{ tex \neg \textsc{na} }}
    % Type lattice
    | Bool                          :: :: Bool          {{ com type lattice }} {{ tex \textsf{Bool} }}
    | Int                           :: :: Int           {{ com type lattice }} {{ tex \textsf{Int} }}
    | Str                           :: :: Str           {{ com type lattice }} {{ tex \textsf{Str} }}

Av {{ tex \widehat{v} }} :: 'Av_' ::=               {{ com abstract value }}
    | [ Lat1 , Lat2 , L ]           :: :: AVal          {{ tex \langle [[Lat1]], [[Lat2]], [[L]] \rangle }}

AE {{ tex \widehat{E} }} :: 'AE_' ::=               {{ com abstract environment }}
    | empty                         :: :: Empty         {{ com empty environment }}
    | AE [ x -> a ]                 :: :: EnvBind       {{ com environment binding }}

AH {{ tex \widehat{H} }} :: 'AH_' ::=               {{ com abstract store }}
    | empty                         :: :: Empty         {{ com empty heap }}
    | AH [ a -> Av ]                :: :: HeapBind      {{ com heap binding }}

AC {{ tex \widehat{C} }} :: 'AC_' ::=               {{ com abstract configuration }}
    | AE AH M                       :: :: EM            {{ tex [[AE]]\,[[AH]]\,[[M]] }}

M :: 'M_' ::=                                       {{ com analysis state }}
    | empty                         :: :: Empty         {{ com empty state }}
    | M [ l -> Lat ]                :: :: StateBind     {{ com state binding }}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%% TERMINALS %%%
terminals :: 'terminals_' ::=
    | isNA          :: :: IsNA          {{ tex \isna }}
    | input()       :: :: Input         {{ tex \ipt }}
    | <-            :: :: Assign        {{ tex \leftarrow }}
    | if            :: :: If            {{ tex \mathsf{if} }}
    | then          :: :: Then          {{ tex \mathsf{then} }}
    | else          :: :: Else          {{ tex \mathsf{else} }}
    | fi            :: :: Fi            {{ tex \mathsf{fi} }}
    | for           :: :: For           {{ tex \mathsf{for} }}
    | in            :: :: In            {{ tex \mathsf{in} }}
    | do            :: :: Do            {{ tex \mathsf{do} }}
    | od            :: :: Od            {{ tex \mathsf{od} }}
    | empty         :: :: Empty         {{ tex \emptyset }}
    | ->            :: :: MapsTo        {{ tex \mapsto }}
    | -se->         :: :: EvalSE        {{ tex \rightharpoonup }}
    | -e->          :: :: EvalE         {{ tex \longrightarrow }}
    | -->           :: :: EvalS         {{ tex \longmapsto }}
    | -rec-se->     :: :: RecSE         {{ tex \overset{\mathrm{rec} }{\rightharpoonup} }}
    | -rec-e->      :: :: RecE          {{ tex \overset{\mathrm{rec} }{\longrightarrow} }}
    | -rec->        :: :: RecS          {{ tex \overset{\mathrm{rec} }{\longmapsto} }}
    | !             :: :: Negate        {{ tex \neg }}
    | *             :: :: Times         {{ tex \times }}
    | /             :: :: Divide        {{ tex \div }}
    | <=            :: :: LEq           {{ tex \leq }}
    | >=            :: :: GEq           {{ tex \geq }}
    | =/=           :: :: NEq           {{ tex \neq }}
    | /\            :: :: And           {{ tex \land }}
    | \/            :: :: Or            {{ tex \lor }}
    | Top           :: :: Top           {{ tex \top }}
    | Bot           :: :: Bot           {{ tex \bot }}
    | union         :: :: Union         {{ tex \cup }}
    | Auop          :: :: Auop          {{ tex \widehat{\circ} }}
    | Abop          :: :: Abop          {{ tex \widehat{\diamond} }}
    | lub           :: :: Lub           {{ tex \sqcup }}
    | glb           :: :: Glb           {{ tex \sqcap }}
    | -na-se->      :: :: NA_SEEval     {{ tex \overset{\textsf{NA} }{\rightsquigarrow}_{se} }}
    | -na-e->       :: :: NA_EEval      {{ tex \overset{\textsf{NA} }{\rightsquigarrow}_{e} }}
    | -na->         :: :: NA_Eval       {{ tex \overset{\textsf{NA} }{\rightsquigarrow} }}
    | -type-se->    :: :: Type_SEEval   {{ tex \overset{\textsf{Type} }{\rightsquigarrow}_{se} }}
    | -type-e->     :: :: Type_EEval    {{ tex \overset{\textsf{Type} }{\rightsquigarrow}_{e} }}
    | -type->       :: :: Type_Eval     {{ tex \overset{\textsf{Type} }{\rightsquigarrow} }}

%%% FORMULAS %%%
formula :: 'formula_' ::=
  | judgement                               :: :: judgement
  | ( formula )                             :: :: parens
  | formula1 /\ formula2                    :: :: and
  | formula1 \/ formula2                    :: :: or
  | v = E ( x )                             :: :: lookup
  | v = [[ uop ]] ( se )                    :: :: primop_uop    {{ tex [[v]] = \llbracket [[uop]] \rrbracket_1 ([[se]]) }}
  | v = [[ bop ]] ( se1 , se2 )             :: :: primop_bop    {{ tex [[v]] = \llbracket [[bop]] \rrbracket_2 ([[se1]], [[se2]]) }}
  | v1 = v2                                 :: :: equal
  | v1 =/= v2                               :: :: not_equal
  | v1 < v2                                 :: :: less_than
  | v1 > v2                                 :: :: greater_than
  | v' = v1 + v2                            :: :: plus
  | v' = v1 - v2                            :: :: minus
  | uop1 = uop2                             :: :: unary_op
  | bop1 = bop2                             :: :: binary_op
  | E' = E [ x -> v ]                       :: :: env_update
  | I = v . I'                              :: :: input_stream  {{ tex [[I]] = [[v]] \cdot [[I']] }}
  | re1 = re2                               :: :: re_equal
  | rse1 = rse2                             :: :: rse_equal
  | a = AE ( x )                            :: :: abstract_env_lookup
  | AE' = AE [ x -> a ]                     :: :: abstract_env_update
  | Av = AH ( a )                           :: :: abstract_heap_lookup
  | AH' = AH [ a -> Av ]                    :: :: abstract_heap_update
  | Av1 = Av2                               :: :: abstract_val_equal
  | fresh ( a )                             :: :: fresh_addr    {{ tex [[a]]~\text{fresh} }}
  | M [ l -> Lat ]                          :: :: update_state
  | M = forall-labs ( l : L ) . formula     :: :: forall_labs   {{ tex [[M]] = \forall [[l]] \in [[L]]\,.\,[[formula]] }}


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%% CONCRETE OPERATIONAL SEMANTICS %%%
defns
operational_semantics :: '' ::=

    %%% Evaluation of simple expressions %%%
    defn
    E se -se-> v :: :: eval_se :: 'SE_'
        {{ com evaluation of simple expressions }}
        {{ tex [[E]]\enspace[[se]] [[-se->]] [[v]] }}
    by

        -----------  :: Literal
        E v -se-> v

        v = E(x)
        --------------  :: Variable
        E x -se-> v

    %%% Evaluation of expressions %%%
    defn
    E e -e-> v :: :: eval_e :: 'E_'
        {{ com evaluation of expressions }}
        {{ tex [[E]]\enspace[[e]] [[-e->]] [[v]] }}
    by

        E se -se-> v
        (v = NA_b) \/ (v = NA_i) \/ (v = NA_s)
        -------------------------------------- :: IsNA
        E isNA se -e-> true

        E se -se-> v
        (v =/= NA_b) /\ (v =/= NA_i) /\ (v =/= NA_s)
        -------------------------------------------- :: IsNotNA
        E isNA se -e-> false

        E se -se-> v
        v' = [[uop]](v)
        ----------------  :: UnaryOp
        E uop se -e-> v'

        E se1 -se-> v1
        E se2 -se-> v2
        v' = [[bop]](v1, v2)
        ---------------------  :: BinaryOp
        E se1 bop se2 -e-> v'

        E se -se-> v
        ------------  :: SimpleExpression
        E se -e-> v

    %%% Evaluation of statements %%%
    defn
    C --> C' :: :: eval_s :: 'S_'
        {{ com evaluation of program states }}
        {{ tex [[C]] [[-->]] [[C']] }}
    by

        E e -e-> v
        E' = E[x -> v]
        ---------------------  :: Assign
        x <- e I E --> v I E'

        I = v . I'
        E' = E[x -> v]
        --------------------------------  :: Input
        x <- l ^ input() I E --> v I' E'

        E se -se-> v
        v = true
        ---------------------------------------  :: IfTrue
        if se then s1 else s2 fi I E --> s1 I E

        E se -se-> v
        v = false
        ---------------------------------------  :: IfFalse
        if se then s1 else s2 fi I E --> s2 I E

        E se1 -se-> v1
        E se2 -se-> v2
        v1' = v1 + 1
        E' = E[x -> v1]
        v1 < v2
        ---------------------------------------------------------------------  :: ForIncr
        for x in se1 : se2 do s od I E --> s ; for x in v1' : v2 do s od I E'

        E se1 -se-> v1
        E se2 -se-> v2
        v1' = v1 - 1
        E' = E[x -> v1]
        v1 > v2
        ---------------------------------------------------------------------  :: ForDecr
        for x in se1 : se2 do s od I E --> s ; for x in v1' : v2 do s od I E'

        E se1 -se-> v1
        E se2 -se-> v2
        E' = E[x -> v1]
        v1 = v2
        -----------------------------------------  :: ForEnd
        for x in se1 : se2 do s od I E --> s I E'

        E e -e-> v
        ---------------  :: Expression
        e I E --> v I E

        s1 I E --> s1' I' E'
        ------------------------------  :: Seq1
        s1 ; s2 I E --> s1' ; s2 I' E'

        ---------------------  :: Seq2
        v ; s2 I E --> s2 I E

%%% RECORDING SEMANTICS %%%
defns
record :: '' ::=

    defn
    E se -rec-se-> rse :: :: rec_se :: 'RecSE_'
        {{ com recording a simple expression stores the result of that simple expression }}
        {{ tex [[E]]\enspace[[se]] [[-rec-se->]] [[rse]] }}
    by

        --------------------  :: Literal
        E v -rec-se-> (v, v)

        v = E(x)
        --------------------  :: Variable
        E x -rec-se-> (x, v)

    defn
    E e -rec-e-> ( re , v ) :: :: rec_e :: 'RecE_'
        {{ com recording an expresion stores the result of that expression }}
        {{ tex [[E]]\enspace[[e]] [[-rec-e->]] ([[re]], [[v]]) }}
    by

        E se -rec-se-> rse
        rse = (se, v)
        (v = NA_b) \/ (v = NA_i) \/ (v = NA_s)
        -------------------------------------- :: IsNA
        E isNA se -rec-e-> (isNA rse, true)

        E se -rec-se-> rse
        rse = (se, v)
        (v =/= NA_b) /\ (v =/= NA_i) /\ (v =/= NA_s)
        -------------------------------------------- :: IsNotNA
        E isNA se -rec-e-> (isNA rse, false)

        E se -rec-se-> rse
        rse = (se, v)
        v' = [[uop]](v)
        -------------------------------  :: UnaryOp
        E uop se -rec-e-> (uop rse, v')

        E se1 -rec-se-> rse1
        E se2 -rec-se-> rse2
        rse1 = (se1, v1)
        rse2 = (se2, v2)
        v' = [[bop]](v1, v2)
        ------------------------------------------  :: BinaryOp
        E se1 bop se2 -rec-e-> (rse1 bop rse2, v')

        E se -rec-se-> rse
        rse = (se, v)
        ----------------------  :: SimpleExpression
        E se -rec-e-> (rse, v)

    defn
    rC -rec-> rC' :: :: rec_s :: 'RecS_'
        {{ com recording a program state updates the execution trace }}
        {{ tex [[rC]] [[-rec->]] [[rC']] }}
    by

        E e -rec-e-> ( re , v )
        E' = E[x -> v]
        ------------------------------------------------  :: Assign
        x <- e I E T -rec-> v I E' T . [x <- (re, v) E']

        I = v . I'
        E' = E[x -> v]
        --------------------------------------------------------------------  :: Input
        x <- l ^ input() I E T -rec-> v I' E' T . [x <- (l ^ input(), v) E']

        E se -rec-se-> rse
        rse = (se, true)
        -------------------------------------------------------------------------------  :: IfTrue
        if se then s1 else s2 fi I E T -rec-> s1 I' E' T . [if rse then . else . fi E']

        E se -rec-se-> rse
        rse = (se, false)
        -------------------------------------------------------------------------------  :: IfFalse
        if se then s1 else s2 fi I E T -rec-> s2 I' E' T . [if rse then . else . fi E']

        E se1 -rec-se-> rse1
        E se2 -rec-se-> rse2
        rse1 = (se1, v1)
        rse2 = (se2, v2)
        v1' = v1 + 1
        E' = E[x -> v1]
        v1 < v2
        ----------------------------------------------------------------------------------------------------------------------  :: ForIncr
        for x in se1 : se2 do s od I E T -rec-> s ; for x in v1' : v2 do s od I E' T . [for (x, v1) in rse1 : rse2 do . od E']

        E se1 -rec-se-> rse1
        E se2 -rec-se-> rse2
        rse1 = (se1, v1)
        rse2 = (se2, v2)
        v1' = v1 - 1
        E' = E[x -> v1]
        v1 > v2
        ----------------------------------------------------------------------------------------------------------------------  :: ForDecr
        for x in se1 : se2 do s od I E T -rec-> s ; for x in v1' : v2 do s od I E' T . [for (x, v1) in rse1 : rse2 do . od E']

        E se1 -rec-se-> rse1
        E se2 -rec-se-> rse2
        rse1 = (se1, v1)
        rse2 = (se2, v2)
        E' = E[x -> v1]
        v1 = v2
        ------------------------------------------------------------------------------------------  :: ForEnd
        for x in se1 : se2 do s od I E T -rec-> s I E' T . [for (x, v1) in rse1 : rse2 do . od E']

        E e -rec-e-> (re, v)
        ------------------------------------  :: Expression
        e I E T -rec-> v I E T . [(re, v) E]

        s1 I E T -rec-> s1' I' E' T'
        --------------------------------------  :: Seq1
        s1 ; s2 I E T -rec-> s1' ; s2 I' E' T'

        ----------------------------------------------  :: Seq2
        v ; s2 I E T -rec-> s2 I E T . [((v, v), v) E]


%%% NA ANALYSIS SEMANTICS %%%
defns
na_analysis :: 'NA_' ::=

    defn
    AE AH rse -na-se-> a Av :: :: na_eval_se :: 'SE_'
        {{ com abstract evaluation of simple expressions returns an address and abstract value }}
        {{ tex [[AE]]\,[[AH]]\enspace[[rse]] [[-na-se->]] [[a]],[[Av]] }}
    by

        Av = [alpha(v), alpha(v), empty]
        fresh(a)
        --------------------------------  :: Literal
        AE AH (v, v) -na-se-> a Av

        a = AE(x)
        Av = AH(a)
        --------------------------  :: Variable
        AE AH (x, v) -na-se-> a Av

    defn
    AE AH ( re , v ) -na-e-> a Av :: :: na_eval_e :: 'E_'
        {{ com abstract evaluation of expressions returns an address and abstract value }}
        {{ tex [[AE]]\,[[AH]]\enspace([[re]],[[v]]) [[-na-e->]] [[a]],[[Av]] }}
    by

        Av = [!NA, !NA, empty]
        fresh(a)
        --------------------------------  :: IsNA
        AE AH (isNA rse, v) -na-e-> a Av

        AE AH rse -na-se-> a [Lat1, Lat2, L]
        Av = [alpha(v), Auop Lat2, L]
        fresh(a')
        -----------------------------------  :: UnaryOp
        AE AH (uop rse, v) -na-e-> a' Av

        AE AH rse1 -na-se-> a1 [Lat11, Lat12, L1]
        AE AH rse2 -na-se-> a2 [Lat21, Lat22, L2]
        Av = [alpha(v), Lat12 Abop Lat22, L1 union L2]
        fresh(a)
        ----------------------------------------------  :: BinaryOp
        AE AH (rse1 bop rse2, v) -na-e-> a Av

        AE AH rse -na-se-> a Av
        ---------------------------  :: SimpleExpression
        AE AH (rse, v) -na-e-> a Av

    defn
    T AC -na-> T' AC' :: :: na_analysis :: 'S_'
        {{ com analysis of execution trace updates the abstract environment and analysis state }}
        {{ tex [[T]]~[[AC]] [[-na->]] [[T']]~[[AC']] }}
    by

        AE AH (re, v) -na-e-> a Av
        AE' = AE[x -> a]
        AH' = AH[a -> Av]
        ----------------------------------------------  :: Assign
        [x <- (re, v) E] . T AE AH M -na-> T AE' AH' M

        Av = [alpha(v), Top, {l}]
        fresh(a)
        AE' = AE[x -> a]
        AH' = AH[a -> Av]
        -------------------------------------------------------  :: Input
        [x <- (l ^ input(), v) E] . T AE AH M -na-> T AE' AH' M

        AE AH rse -na-se-> a Av
        Av = [Lat1, Lat2, L]
        Av' = [Lat1, !NA, L]
        AH' = AH[a -> Av']
% TODO: this notation is ugly
        M' = forall-labs (l : L) . M[l -> !NA]
        ---------------------------------------------------------  :: If
        [if rse then . else . fi E] . T AE AH M -na-> T AE AH' M'

        AE AH rse1 -na-se-> a1 Av1
        AE AH rse2 -na-se-> a2 Av2
        Av1 = [Lat11, Lat12, L1]
        Av2 = [Lat21, Lat22, L2]
        Av1' = [Lat11, !NA, L1]
        Av2' = [Lat21, !NA, L2]
        AH' = AH[a1 -> Av1']
        AH'' = AH'[a2 -> Av2']
% TODO: this notation is ugly
        M' = forall-labs (l : L1 union L2) . M[l -> !NA]
        -------------------------------------------------------------------  :: For
        [for (x, v) in rse1 : rse2 do . od E] . T AE AH M -na-> T AE AH' M'

        ---------------------------------------  :: Expression
        [(re, v) E] . T AE AH M -na-> T AE AH M


%%% TYPE ANALYSIS SEMANTICS %%%
defns
type_analysis :: 'Type_' ::=

    defn
    AE AH rse -type-se-> a Av :: :: type_eval_se :: 'SE_'
        {{ com abstract evaluation of simple expressions returns an address and abstract value }}
        {{ tex [[AE]]\,[[AH]]\enspace[[rse]] [[-type-se->]] [[a]],[[Av]] }}
    by

        Av = [alpha(v), alpha(v), empty]
        fresh(a)
        --------------------------------  :: Literal
        AE AH (v, v) -type-se-> a Av

        a = AE(x)
        Av = AH(a)
        --------------------------  :: Variable
        AE AH (x, v) -type-se-> a Av

    defn
    AE AH ( re , v ) -type-e-> a Av :: :: type_eval_e :: 'E_'
        {{ com abstract evaluation of expressions returns an address and abstract value }}
        {{ tex [[AE]]\,[[AH]]\enspace([[re]],[[v]]) [[-type-e->]] [[a]],[[Av]] }}
    by

        Av = [Bool, Bool, empty]
        fresh(a)
        --------------------------------  :: IsNA
        AE AH (isNA rse, v) -type-e-> a Av

        AE AH rse -type-se-> a Av1
        Av1 = [Lat1, Lat2, L]
        Av1' = [Lat1, Bool, L]
% TODO: need to propagate Av1' constraint and update M
        Av' = [alpha(v), Bool, L]
        fresh(a')
        uop = !
        -----------------------------------  :: UnaryOpBool
        AE AH (uop rse, v) -type-e-> a' Av'

        AE AH rse -type-se-> a Av1
        Av1 = [Lat1, Lat2, L]
        Av1' = [Lat1, Int, L]
% TODO: need to propagate Av1' constraint and update M
        Av' = [alpha(v), Int, L]
        fresh(a')
        (uop = +) \/ (uop = -)
        -----------------------------------  :: UnaryOpInt
        AE AH (uop rse, v) -type-e-> a' Av'

        AE AH rse1 -type-se-> a1 Av1
        Av1 = [Lat11, Lat12, L1]
        Av1' = [Lat11, Bool, L1]
% TODO: need to propagate Av1' constraint and update M
        AE AH rse2 -type-se-> a2 Av2
        Av2 = [Lat21, Lat22, L2]
        Av2' = [Lat21, Bool, L2]
% TODO: need to propagate Av2' constraint and update M
        Av = [alpha(v), Bool, L1 union L2]
        fresh(a)
        (bop = /\) \/ (bop = \/)
        ---------------------------------------  :: BinaryOpBool
        AE AH (rse1 bop rse2, v) -type-e-> a Av

        AE AH rse1 -type-se-> a1 Av1
        Av1 = [Lat11, Lat12, L1]
        Av1' = [Lat11, Int, L1]
% TODO: need to propagate Av1' constraint and update M
        AE AH rse2 -type-se-> a2 Av2
        Av2 = [Lat21, Lat22, L2]
        Av2' = [Lat21, Int, L2]
% TODO: need to propagate Av2' constraint and update M
        Av = [alpha(v), Int, L1 union L2]
        fresh(a)
        (bop = +) \/ (bop = -) \/ (bop = *) \/ (bop = /) \/ (bop = %) \/ (bop = <) \/ (bop = <=) \/ (bop = >) \/ (bop = >=)
        ----------------------------------------------  :: BinaryOpInt
        AE AH (rse1 bop rse2, v) -type-e-> a Av

        AE AH rse1 -type-se-> a1 Av1
        Av1 = [Lat11, Lat12, L1]
        AE AH rse2 -type-se-> a2 Av2
        Av2 = [Lat21, Lat22, L2]
        Av = [alpha(v), Lat12 Abop Lat22, L1 union L2]
        fresh(a)
        ----------------------------------------------  :: BinaryOp
        AE AH (rse1 bop rse2, v) -type-e-> a Av

        AE AH rse -type-se-> a Av
        -----------------------------  :: SimpleExpression
        AE AH (rse, v) -type-e-> a Av

    defn
    T AC -type-> T' AC' :: :: type_analysis :: 'S_'
        {{ com analysis of execution trace updates the abstract environment and analysis state }}
        {{ tex [[T]]~[[AC]] [[-type->]] [[T']]~[[AC']] }}
    by

        AE AH (re, v) -type-e-> a Av
        AE' = AE[x -> a]
        AH' = AH[a -> Av]
        ------------------------------------------------  :: Assign
        [x <- (re, v) E] . T AE AH M -type-> T AE' AH' M

        Av = [alpha(v), Top, {l}]
        fresh(a)
        AE' = AE[x -> a]
        AH' = AH[a -> Av]
        ---------------------------------------------------------  :: Input
        [x <- (l ^ input(), v) E] . T AE AH M -type-> T AE' AH' M

        AE AH rse -type-se-> a Av
        Av = [Lat1, Lat2, L]
        Av' = [Lat1, Bool, L]
        AH' = AH[a -> Av']
% TODO: this notation is ugly
        M' = forall-labs (l : L) . M[l -> Bool]
        -----------------------------------------------------------  :: If
        [if rse then . else . fi E] . T AE AH M -type-> T AE AH' M'

        AE AH rse1 -type-se-> a1 Av1
        AE AH rse2 -type-se-> a2 Av2
        Av1 = [Lat11, Lat12, L1]
        Av2 = [Lat21, Lat22, L2]
        Av1' = [Lat11, Int, L1]
        Av2' = [Lat21, Int, L2]
        AH' = AH[a1 -> Av1']
        AH'' = AH'[a2 -> Av2']
% TODO: this notation is ugly
        M' = forall-labs (l : L1 union L2) . M[l -> Int]
        -------------------------------------------------------------------  :: For
        [for (x, v) in rse1 : rse2 do . od E] . T AE AH M -type-> T AE AH' M'

        ---------------------------------------  :: Expression
        [(re, v) E] . T AE AH M -type-> T AE AH M
